use dummy as knowledge.

order = linearall.
module green {
	%Build green and parks to satisfy green indicator
	
	%Get the ID of the correct indicator and store it in the belief base.
	if bel(not(greenId(_,StakeholderID)), my_stakeholder_id(StakeholderID),indicatorLink(StakeholderID,LinkIndicator),
		member(indicatorWeights(IndicatorID,'Gemeente Groen', IndicatorWeight),LinkIndicator))
		then insert(greenId(IndicatorID,StakeholderID)).
	
	%If the green indicator in a Zone is below 0.9
	%and if we have a suitable location to build a park
	%and if we don't already have a goal of building in that zone
	%and if we have not already attempted to build on that multipolygon
	%then adopt the goal of building a stadspark at that location.
	if bel(greenId(GreenIndicatorID,OurID),
		indicator(GreenIndicatorID,_,_,ZoneLinkList), member(zone_link(ZoneID,GreenIndicatorID,Current,Target),ZoneLinkList),
		Target =\= 0, 0.95 > (Current / Target), land(_,OurID,MultiPolygon,ZoneIDList,Size), member(ZoneID,ZoneIDList),
		Size < 20000),
		not(goal(buildPark(ZoneID,_))), not(bel(attempttoBuild(MultiPolygon)))
		then adopt(buildPark(ZoneID, MultiPolygon)).
}