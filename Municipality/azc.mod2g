use dummy as knowledge.
use tygron as actionspec.

order = linearrandom.
module azc{
	%if we have build an azc in the last ten cycles wait for indicators to update
	if bel(azc_timeout(X), X>0, Y is X-1) then
		delete(azc_timeout(X)) + insert(azc_timeout(Y)).
	if bel(azc_timeout(X), X>0) then
		exit-module.
	
	%if we already have a goal for building a specific azc we exit.
	if goal(buildAZC(_,_)) then exit-module.

	%If we have no list of available land get it from the connector.
	if bel(not(azc_area(0,List)),
		%do we have enough money
		money(X), X>(750*20*Azc),
		%find maximal neccesary size
		azc(Azc), Azc>0,
		MaxArea is 20*Azc+1000,
		MinArea is max(MaxArea/4,500),
		%find a zone that support an azc and that we own land in
		my_stakeholder_id(StakeholderID),
		land(_,StakeholderID,_,ZoneList,_),
		member(Zone,ZoneList),
		zone(Zone,_,_,_,Categories),
		member('SOCIAL',Categories))
		%we want up to 5 polygons
		then get_relevant_areas(0, build, [amount(5), area(MinArea, MaxArea), zones(Zone)]).
	
	%Build an azc in one of the retrieved polygons
	if bel(
		%first check if there still is demand
		azc(Azc), Azc>0,
		%grab a polygon from our list
		azc_area(0,[[Poly|Y]|Rest])	,
		%Check if we have the funds to build an azc
		money(X), X>(1200*20*Azc))
		then adopt(buildAZC(Poly)) + 
		%update timeout
		delete(azc_timeout(0)) + insert(azc_timeout(10)) +
		%remove building option
		delete(azc_area(0,[[Poly|Y]|Rest])) +
		insert(azc_area(0,Rest)).
	
	%Delete the remaining list if building failed
	if bel(azc_area(0,List))
		then delete(azc_area(0,List)).
}